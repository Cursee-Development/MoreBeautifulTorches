plugins {
    id "org.jetbrains.gradle.plugin.idea-ext" version "1.1.7"
    id "io.github.pacifistmc.forgix" version "1.2.9"
}

forgix {
    group = "${project.group}" // same as targetting ${group}, just not sure if expandProps occurs before this
    mergedJarName = "${mod_id}-merged-${minecraft_version}-${version}.jar"
    outputDir = "build/libs"

    custom {
        projectName = "common"
        jarLocation = "build/libs/${mod_id}-common-${minecraft_version}-${version}.jar"
    }

    fabric {
        projectName = "fabric"
        jarLocation = "build/libs/${mod_id}-fabric-${minecraft_version}-${version}.jar"
    }

    neoforge {
        projectName = "neoforge"
        jarLocation = "build/libs/${mod_id}-neoforge-${minecraft_version}-${version}.jar"

        mixin "${mod_id}.neoforge.mixins.json"
    }

}

subprojects {
    apply plugin: 'java'

    java.toolchain.languageVersion = JavaLanguageVersion.of(21)

    jar {
        manifest {
            attributes([
                    'Specification-Title'     : mod_name,
                    'Specification-Vendor'    : mod_author,
                    'Specification-Version'   : project.jar.archiveVersion,
                    'Implementation-Title'    : project.name,
                    'Implementation-Version'  : project.jar.archiveVersion,
                    'Implementation-Vendor'   : mod_author,
                    'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                    'Timestamp'               : System.currentTimeMillis(),
                    'Built-On-Java'           : "${System.getProperty('java.vm.version')} (${System.getProperty('java.vm.vendor')})",
                    'Built-On-Minecraft'      : minecraft_version
            ])
        }
    }

    repositories {
        mavenCentral()
        maven {
            name = 'Sponge / Mixin'
            url = 'https://repo.spongepowered.org/repository/maven-public/'
        }
        maven {
            url "https://cursemaven.com"
            content {
                includeGroup "curse.maven"
            }
        }
    }

    tasks.withType(JavaCompile).configureEach {

        it.options.encoding = 'UTF-8'
        it.options.getRelease().set(21)
    }

    tasks.withType(GenerateModuleMetadata).configureEach {

        enabled = false
    }

    tasks.withType(Jar).all {

        duplicatesStrategy 'include'
    }
}

//idea {
//    module {
//        downloadSources = true
//        downloadJavadoc = true
//    }
//}